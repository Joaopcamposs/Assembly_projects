#include "p16f877a.inc"
; __config 0xFF31
 __CONFIG _FOSC_XT & _WDTE_OFF & _PWRTE_ON & _BOREN_OFF & _LVP_OFF & _CPD_OFF & _WRT_OFF & _CP_OFF
 
#define BANCO0	BCF STATUS,RP0
#define BANCO1	BSF STATUS,RP0
 
 ;variaveis
 CBLOCK  0x20
    FLAGS
    UNIDADE
    DEZENA
    CENTENA
    MILHAR
    CODIGO
    W_TEMP
    S_TEMP
    TEMPO_01S
 ENDC

#define	FIM_1MS	    FLAGS,0
#define FIM_01S	    FLAGS,1
#define CONTANDO    FLAGS,2
#define ORDEM	    FLAGS,3
#define BTCONTI	    PORTB,0
#define BTCONTD	    PORTB,1
#define BTSTOP	    PORTB,2
#define	BTZERO	    PORTB,3
#define	DISPLAY	    PORTD
#define D_UNIDADE   PORTB,4
#define D_DEZENA    PORTB,5
#define D_CENTENA   PORTB,6
#define D_MILHAR    PORTB,7
    
V_TEMPO	    equ .100
V_TMR0	    equ	.131

RES_VECT  CODE    0x0000
    GOTO    INICIO

INT_VECT  CODE    0x0004
    MOVWF   W_TEMP
    MOVF    STATUS,W
    MOVWF   S_TEMP
    BTFSS   INTCON,T0IF
    GOTO    SAI_INTERRUPCAO
    BCF	    INTCON,T0IF
    MOVLW   V_TMR0
    ADDWF   TMR0,F
    BSF	    FIM_1MS
    DECFSZ  TEMPO_01S,F
    GOTO    SAI_INTERRUPCAO
    MOVLW   V_TEMPO
    MOVWF   TEMPO_01S
    BSF	    FIM_01S
SAI_INTERRUPCAO
    MOVF    S_TEMP,W
    MOVWF   STATUS
    MOVF    W_TEMP,W
    RETFIE

    
INICIO
    BANCO1
    CLRF    TRISD
    BCF	    TRISB,4
    BCF	    TRISB,5
    BCF	    TRISB,6
    BCF	    TRISB,7
    MOVLW   B'11010001'
    MOVWF   OPTION_REG
    BANCO0
    CLRF    DISPLAY
    CLRF    UNIDADE
    CLRF    DEZENA
    CLRF    CENTENA
    CLRF    MILHAR
    BCF	    FIM_1MS
    BCF	    FIM_01S
    BCF	    CONTANDO
    CLRF    CODIGO
    BSF	    D_UNIDADE
    BCF	    D_DEZENA
    BCF	    D_CENTENA
    BCF	    D_MILHAR
    MOVLW   V_TEMPO
    MOVWF   TEMPO_01S
    MOVLW   V_TMR0
    MOVWF   TMR0
    BSF	    INTCON,T0IE
    BSF	    INTCON,GIE
MAIN_LOOP
    BTFSC   FIM_1MS
    CALL    ATUALIZA_DISPLAY
    BTFSC   CONTANDO
    GOTO    VERIFICA_STOP
    BTFSS   BTCONTI
    GOTO    INCREMENTA
    BTFSS   BTCONTD
    GOTO    DECREMENTA
    BTFSC   BTZERO
    GOTO    MAIN_LOOP
    CLRF    UNIDADE
    CLRF    DEZENA
    CLRF    CENTENA
    CLRF    MILHAR
    GOTO    MAIN_LOOP
VERIFICA_STOP
    BTFSC   BTSTOP
    GOTO    VERIFICA_01S
    BCF	    CONTANDO
    BCF	    ORDEM
    GOTO    MAIN_LOOP
VERIFICA_01S
    BTFSS   FIM_01S
    GOTO    MAIN_LOOP
    BCF	    FIM_01S
    BTFSS   ORDEM
    GOTO    REALIZA_INC
    MOVLW   .1
    SUBWF   UNIDADE,F
    BTFSS   STATUS,C
    GOTO    DECREMENTA_DEZENA
    GOTO    MAIN_LOOP
DECREMENTA_DEZENA
    MOVLW   .9
    MOVWF   UNIDADE
    MOVLW   .1
    SUBWF   DEZENA,F
    BTFSS   STATUS,C
    GOTO    DECREMENTA_CENTENA
    GOTO    MAIN_LOOP
DECREMENTA_CENTENA
    MOVLW   .9
    MOVWF   DEZENA
    MOVLW   .1
    SUBWF   CENTENA,F
    BTFSS   STATUS,C
    GOTO    DECREMENTA_MILHAR
    GOTO    MAIN_LOOP
DECREMENTA_MILHAR
    MOVLW   .9
    MOVWF   CENTENA
    MOVLW   .1
    SUBWF   MILHAR,F
    BTFSC   STATUS,C
    GOTO    MAIN_LOOP
    MOVLW   .9
    MOVWF   MILHAR
    GOTO    MAIN_LOOP
REALIZA_INC    
    INCF    UNIDADE,F
    MOVLW   .10
    SUBWF   UNIDADE,W
    BTFSC   STATUS,C
    GOTO    INCREMENTA_DEZENA
    GOTO    MAIN_LOOP
INCREMENTA_DEZENA
    CLRF    UNIDADE
    INCF    DEZENA,F
    MOVLW   .10
    SUBWF   DEZENA,W
    BTFSC   STATUS,C
    GOTO    INCREMENTA_CENTENA
    GOTO    MAIN_LOOP
INCREMENTA_CENTENA
    CLRF    DEZENA
    INCF    CENTENA,F
    MOVLW   .10
    SUBWF   CENTENA,W
    BTFSC   STATUS,C
    GOTO    INCREMENTA_MILHAR
    GOTO    MAIN_LOOP
INCREMENTA_MILHAR
    CLRF    CENTENA
    INCF    MILHAR,F
    MOVLW   .10
    SUBWF   MILHAR,W
    BTFSC   STATUS,C
    CLRF    MILHAR
    GOTO    MAIN_LOOP
INCREMENTA
    BCF	    ORDEM
    GOTO    ATIVA_CONTANDO
DECREMENTA
    BSF	    ORDEM
    GOTO    ATIVA_CONTANDO
ATIVA_CONTANDO
    BCF	    FIM_01S
    MOVLW   V_TEMPO
    MOVWF   TEMPO_01S
    BSF	    CONTANDO
    MOVLW   V_TMR0
    MOVWF   TMR0
    GOTO    MAIN_LOOP
ATUALIZA_DISPLAY
    BCF	    FIM_1MS
    BTFSS   D_UNIDADE
    GOTO    VER_DEZENA
    BCF	    D_UNIDADE
    MOVF    DEZENA,W
    CALL    BUSCA_CODIGO
    IORLW   .128
    MOVWF   DISPLAY
    BSF	    D_DEZENA
    RETURN
VER_DEZENA
    BTFSS   D_DEZENA
    GOTO    VER_CENTENA
    BCF	    D_DEZENA
    MOVF    CENTENA,W
    CALL    BUSCA_CODIGO
    MOVWF   DISPLAY
    BSF	    D_CENTENA
    RETURN
VER_CENTENA
    BTFSS   D_CENTENA
    GOTO    VER_MILHAR
    BCF	    D_CENTENA
    MOVF    MILHAR,W
    CALL    BUSCA_CODIGO
    MOVWF   DISPLAY
    BSF	    D_MILHAR
    RETURN
VER_MILHAR
    BCF	    D_MILHAR
    MOVF    UNIDADE,W
    CALL    BUSCA_CODIGO
    MOVWF   DISPLAY
    BSF	    D_UNIDADE
    RETURN
BUSCA_CODIGO
    ADDWF   PCL,F
    RETLW   0x3F
    RETLW   0x6
    RETLW   0x5B
    RETLW   0x4F
    RETLW   0x66
    RETLW   0x6D
    RETLW   0x7D
    RETLW   0x7
    RETLW   0x7F
    RETLW   0x6F
    END